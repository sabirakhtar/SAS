*** SUBSTR Function ***;
data VIN;
   length VIN $ 17;
   VIN='1F1JF27W86J178227';
   Country=substr(VIN,1,1);
   Make=substr(VIN,2,1);
   VehicleType=substr(VIN,3,1);
   Features=substr(VIN,4,5);
   ModelYear=substr(VIN,10,1);
   AssemblyPlant=substr(VIN,11,1);
   SequenceNumber=substr(VIN,12,6);
run;
proc print;
run;

*** SCAN Function ***;
data test;
   length six two $ 22;
   Sentence='This is a test, a very short test.';
   Six=scan(Sentence,6,' ');
   Two=scan(Sentence,2,',');
run;
proc print;
run; 

data test;
   Sentence='This+is an(ex-tremely)**crazy**test!';
   Four=scan(Sentence,4);
   Two=scan(Sentence,2);
run;
proc print;
run;

data test;
   Sentence='$%This+is an(ex-tremely)**crazy**test!';
   One=scan(Sentence,1);
   Six=scan(Sentence,6);
   Eight=scan(Sentence,8);
   MinusTwo=scan(Sentence,-2);
   Two=scan(Sentence,2,'*');
run;
proc print;
run;

*** LEFT and RIGHT Functions ***;
data align;
   Var='   ZOOLOGY   ';
   NewVar1=left(Var);
   NewVar2=right(Var);
   NewVar3=substr(left(Var),1,3);
run;
proc print;
run;
proc contents;
run;

*** Concatenation ***;
data name1;
   length Name $ 20 Last First Middle $ 10;
   Name = 'Jones, Mary Ann, Sue';
   Last = scan(name,1,',');
   First = left(scan(Name,2,','));
   Middle = left(scan(Name,3,','));
   FullName = trim(First) || trim(Middle) || Last;
   Name1=trim(Middle);
   Name2=trim(First)||' '||trim(Middle)||' '||Last;
run;
proc print;
run;
proc contents;
run; 

data name2;
   length First Middle Last $ 10;
   First = 'Tony';
   Middle = '  Albert';
   Last = 'Smith';
   Name1 = cat(First, Middle, Last);
   Name2 = cats(First, Middle, Last);
   Name3 = catt(First, Middle, Last);
   Name4 = catx(' ', First, Middle, Last);
run;
proc print;
run;
proc contents;
run; 

data location;
   length City State $ 15 CityState $ 30; 
   City='Princeton';
   State='New Jersey';
   CityState1=cats(City, State);
   CityState2=cats(', ', City, State);
   CityState3=catx(',', City, State);
   CityState4=catx(', ', City, State);
run;
proc print;
run;
proc contents;
run; 

*** TRANWRD ***;
data function;
   Sentence='Functions are very hard to use.';
   NewSentence1=tranwrd(Sentence,'hard','easy');
   NewSentence2=tranwrd(Sentence,'hard','difficult');
   NewSentence3=tranwrd(Sentence,'HARD','FUN');
run;
proc print;
run;
proc contents;
run;

*** INDEX ***;
data tonguetwister;
   string='How much WOOD would a woodchuck chuck';
   substring1='WOOD';
   num1=index(string,substring1);
   substring2='wood';
   num2=index(string,substring2);
run;
proc print;
run;

data tonguetwister;
   string='How much WOOD would a woodchuck chuck';
   num=index(string,'wood ');
run;
proc print;
run;

data tonguetwister;
   string='How much WOOD would a woodchuck chuck';
   num=index(string,' wo');
run;
proc print;
run;

*** FIND ***;
*** Exercise 2 ***;
data tonguetwister;
   length string $ 37;
   string='How much WOOD would a woodchuck chuck';
   num1=find(string,'wood');
   num2=find(string,'wood','i');
   num3=find(string,'wood ','t');
   num4=find(string,'wood   ','it');
   num5=find(string,'WOOD ','it',15);
   num6=find(string,'WOOD ','it',-15);
   num7=find(string,'wood',40);
   num8=find(string,'WOOD','i',-40);
run;
proc print;
run;

*** Case Function ***;
data names;
   name='Jane SMITH-JONES';
   name1=lowcase(name);
   name2=upcase(name);
   name3=propcase(name);
   name4=propcase(name,' ');
run;
proc print;
run;

data example;
   Var='r&d, u.s. division (not puerto rico)'; 
   NewVar=propcase(Var);
   NewVar1=propcase(Var,'.&( ');
run;
proc print;
run;
proc contents;
run;

*** State Functions ***;
data test;
   state=' IL';
   newstate=stnamel(state);
run;
proc print data=state;
run;

*** Date Functions ***;
data birthday;
   BirthDate=894;  
   BirthWeekDay=weekday(BirthDate);
   BirthDay=day(BirthDate);
   BirthMonth=month(BirthDate);
   BirthQtr=qtr(BirthDate);
   BirthYear=year(BirthDate);
   output;
run;
proc print data=birthday noobs;
   format BirthDate weekdate.;
run;

data new;
   current='15dec2007'd;
run;
proc print;
run;

data new;
   m=3;
   d=27;
   y=1982;
   Date=mdy(m,d,y);
run;
proc print;
run;

data birthday;
   birth='29nov1970'd;
   age=yrdif(birth,'02feb2007'd,'act/act');
   output;
   birth='21jan1970'd;
   age=yrdif(birth,'02feb2007'd,'act/act');
   output;
   birth='05MAY1999'd;
   age=yrdif(birth,'10NOV1999'd,'act/act');
   output;
run;
proc print;
run;

*** Truncation Functions ***;
*** Exercise 3 ***;
data trunc;
   num=2.75;
   a=ceil(num);
   b=floor(num);
   c=round(num);
   d=int(num);
   output;
   num=-2.75;
   a=ceil(num);
   b=floor(num);
   c=int(num);
   d=round(num);
   output;
   num=23.1234;
   a=ceil(num);
   b=floor(num);
   c=int(num);
   d=round(num);
   output;
   num=-23.1234;
   a=ceil(num);
   b=floor(num);
   c=int(num);
   d=round(num);
   output;
   num=29;
   a=ceil(num);
   b=floor(num);
   c=int(num);
   d=round(num);
   output;
run;
proc print;
run;

data round;
   num=986.151; 
   C=round(num,100);  
   D=round(num,.01);
   output;
run;
proc print;
run;

*** Descriptive Statistics ***;
data math;
   var1=2;
   var2=6;
   var3=.;
   var4=4;
   maximum = max(var1,var2,var3,var4);
   average = mean(var1,var2,var3,var4);
   minimum = min(var1,var2,var3,var4);
   total = sum(var1,var2,var3,var4);
run;
proc print;
run;
  
data math;
   var1=2;
   var2=6;
   var3=.;
   var4=4;
   maximum = max(of var1-var4);
   average = mean(of var1-var4);
   minimum = min(of var1-var4);
   total = sum(of var1-var4);
run;
proc print;
run;

data math;
   var1=30;
   var2=15;
   var3=10;
   total = sum(var1-var3);
run;
proc print;
run;

